group 'nrwDatenStrukturenTests'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

test {
    useTestNG()

    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true

    // set heap size for the test JVM(s)
    minHeapSize = "128m"
    maxHeapSize = "512m"

    // listen to events in the test execution lifecycle
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

    // listen to standard out and standard error of the test JVM(s)
    onOutput { descriptor, event ->
        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
    }
}

dependencies {
    compile group: 'org.testng', name: 'testng', version: '6.9.10'

    //An assertion library that is better than JUnit defaults
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
}

import org.apache.tools.ant.taskdefs.condition.Os
task showReport(type:Exec) {
    workingDir './build/reports/tests/test/'

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        //on windows:
        commandLine 'cmd', '/c', 'start index.html'
    } else {
        commandLine 'sh', '-c', 'open index.html'
    }
}

